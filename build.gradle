plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.20'
    id 'nu.studer.jooq' version '9.0'
    id "org.flywaydb.flyway" version "9.16.3"
}

group = 'com.gildedrose'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

ext.junitVersion = '5.9.1'
ext.jacksonVersion = '2.14.1'
ext.testJdbcUrl = providers.environmentVariable('JDBC_URL').orElse('jdbc:h2:~/gilded-rose-test-db').get()
ext.databaseUsername = providers.environmentVariable('DB_USERNAME').orElse('gilded').get()
ext.databasePassword = providers.environmentVariable('DB_PASSWORD').orElse('rose').get()


dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "dev.forkhandles:result4k:2.3.0.0"
    implementation "org.slf4j:slf4j-jdk14:1.7.36"

    implementation platform("org.http4k:http4k-bom:5.6.0.0")
    implementation "org.http4k:http4k-core"
    implementation "org.http4k:http4k-server-undertow"
    implementation "org.http4k:http4k-cloudnative"
    implementation "org.http4k:http4k-client-apache"
    implementation "org.http4k:http4k-template-handlebars"
    implementation "org.http4k:http4k-client-apache"

    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"

    implementation 'com.h2database:h2:2.2.224'
    implementation "com.zaxxer:HikariCP:5.0.1"

    implementation "org.jooq:jooq:3.19.1"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "io.strikt:strikt-core:0.34.1"

    testImplementation 'com.microsoft.playwright:playwright:1.36.0'

    testImplementation "org.http4k:http4k-testing-approval"
    testImplementation "org.http4k:http4k-testing-hamkrest"
    testImplementation "org.http4k:http4k-testing-strikt"

    jooqGenerator 'com.h2database:h2:2.2.224'
}

test {
    useJUnitPlatform()
}


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

compileKotlin {
    compilerOptions.freeCompilerArgs.add("-Xcontext-receivers")
}

compileTestKotlin {
    compilerOptions.freeCompilerArgs.add("-Xcontext-receivers")
}

flyway {
    url = "$testJdbcUrl"
    user = "$databaseUsername"
    password = "$databasePassword"
}

jooq {
    version = '3.19.1'  // default (can be omitted)
    edition = nu.studer.gradle.jooq.JooqEdition.OSS  // default (can be omitted)

    configurations {
        main {  // name of the jOOQ configuration
            generateSchemaSourceOnCompilation = true  // default (can be omitted)

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.h2.Driver'
                    url = "$testJdbcUrl"
                    user = "$databaseUsername"
                    password = "$databasePassword"
                    properties {
                        property {
                            key = 'ssl'
                            value = 'false'
                        }
                    }
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.h2.H2Database'
                        inputSchema = 'PUBLIC'
                        forcedTypes {
                            forcedType {
                                name = 'instant'
                                includeExpression = '.*'
                                includeTypes = 'TIMESTAMP WITH TIME ZONE'
                            }
                        }
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'com.gildedrose.db'
                        directory = 'build/generated-src/jooq/main'  // default (can be omitted)
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

tasks.named('generateJooq').configure { allInputsDeclared = true }
